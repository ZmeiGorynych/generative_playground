

\section{Full Listing of the Grammar}\label{app:grammar}
\small
\begin{verbatim}
smiles -> nonH_bond
smiles -> initial_valence_1
smiles -> initial_valence_1 nonH_bond
smiles -> initial_valence_2 double_bond
smiles -> initial_valence_3 triple_bond
initial_valence_1 -> 'F'
initial_valence_1 -> 'Cl'
initial_valence_1 -> 'Br'
initial_valence_1 -> 'I'
initial_valence_1 -> '[' 'O' '-' ']'
initial_valence_1 -> '[' 'N' 'H' '3' '+' ']'
initial_valence_2 -> 'O'
initial_valence_2 -> 'S'
initial_valence_3 -> '[' 'C' '@' 'H' ']'
initial_valence_3 -> '[' 'C' '@' '@' 'H' ']'
initial_valence_3 -> 'N'
initial_valence_3 -> '[' 'N' 'H' '+' ']'
nonH_bond -> valence_1
nonH_bond -> valence_2
nonH_bond -> valence_2 nonH_bond
nonH_bond -> valence_3 double_bond
nonH_bond -> valence_4 triple_bond
double_bond -> '=' valence_2
double_bond -> '=' valence_3
double_bond -> '=' valence_3 nonH_bond
double_bond -> '=' valence_4 double_bond
triple_bond -> '#' valence_3
triple_bond -> '#' valence_4
triple_bond -> '#' valence_4 nonH_bond
valence_4 -> 'C'
valence_4 -> '[' 'C' '@' ']'
valence_4 -> '[' 'C' '@' '@' ']'
valence_4 -> '[' 'N' '+' ']'
valence_3 -> '[' 'C' '@' 'H' ']'
valence_3 -> '[' 'C' '@' '@' 'H' ']'
valence_3 -> 'N'
valence_3 -> '[' 'N' 'H' '+' ']'
valence_3 -> valence_4
valence_3 -> valence_4 '(' nonH_bond ')'
valence_2 -> 'O'
valence_2 -> 'S'
valence_2 -> 'S' '(' '=' 'O' ')' '(' '=' 'O' ')'
valence_2 -> valence_3
valence_2 -> valence_3 '(' nonH_bond ')'
valence_2 -> valence_4 '(' double_bond ')'
valence_1 -> 'F'
valence_1 -> 'Cl'
valence_1 -> 'Br'
valence_1 -> 'I'
valence_1 -> '[' 'O' '-' ']'
valence_1 -> '[' 'N' 'H' '3' '+' ']'
valence_1 -> valence_2
valence_1 -> valence_2 '(' nonH_bond ')'
valence_1 -> valence_3 '(' double_bond ')'
valence_1 -> valence_4 '(' triple_bond ')'
nonH_bond -> valence_2 slash valence_3 '=' valence_3 slash valence_2
slash -> '/'
slash -> '\'
nonH_bond -> aliphatic_ring
aliphatic_ring -> valence_3_num cycle_bond
aliphatic_ring -> valence_4_num cycle_double_bond
valence_2 -> vertex_attached_ring
vertex_attached_ring -> valence_4_num '(' cycle_bond ')'
cycle_bond -> valence_2 cycle_bond
cycle_bond -> valence_3 cycle_double_bond
cycle_double_bond -> '=' valence_3 cycle_bond
cycle_bond -> valence_2_num
cycle_double_bond -> '=' valence_3_num
nonH_bond -> aliphatic_ring_segment
nonH_bond -> aliphatic_ring_segment nonH_bond
cycle_bond -> aliphatic_ring_segment cycle_bond
aliphatic_ring_segment -> valence_3 '(' cycle_bond ')' valence_3_num
aliphatic_ring_segment -> valence_4 '(' cycle_bond ')' '=' valence_4_num
aliphatic_ring_segment -> valence_4 '(' cycle_double_bond ')' valence_3_num
nonH_bond -> aromatic_ring_5
nonH_bond -> aromatic_ring_6
starting_aromatic_c_num -> 'c' num
aromatic_atom -> 'n'
aromatic_atom -> 'c'
aromatic_atom -> 'c' '(' nonH_bond ')'
aromatic_os -> 'o'
aromatic_os -> 's'
aromatic_os -> 'n' '(' nonH_bond ')'
aromatic_os -> '[' 'n' 'H' ']'
aromatic_atom_num -> 'n' num
aromatic_atom_num -> 'c' num
aromatic_atom_num -> 'c' num nonH_bond
aromatic_os_num -> 'o' num
aromatic_os_num -> 's' num
aromatic_os_num -> 'n' num nonH_bond
nonH_bond -> double_aromatic_ring
double_aromatic_ring -> 'c' num1 aromatic_atom aromatic_atom aromatic_atom 'c' num 'c' num1 aromatic_atom aromatic_atom aromatic_atom aromatic_atom_num
double_aromatic_ring -> 'c' num1 aromatic_atom aromatic_atom aromatic_atom 'c' num 'n' num1 aromatic_atom aromatic_atom aromatic_atom_num
double_aromatic_ring -> 'c' num1 aromatic_atom aromatic_atom aromatic_atom 'n' num 'c' num1 aromatic_atom aromatic_atom aromatic_atom_num
aromatic_ring_6 -> starting_aromatic_c_num aromatic_atom full_aromatic_segment aromatic_atom aromatic_atom_num
aromatic_ring_6 -> starting_aromatic_c_num full_aromatic_segment full_aromatic_segment aromatic_atom_num
aromatic_ring_5 -> starting_aromatic_c_num aromatic_os full_aromatic_segment aromatic_atom_num
aromatic_ring_5 -> starting_aromatic_c_num aromatic_atom aromatic_os aromatic_atom aromatic_atom_num
aromatic_ring_5 -> starting_aromatic_c_num full_aromatic_segment aromatic_os aromatic_atom_num
aromatic_ring_5 -> starting_aromatic_c_num full_aromatic_segment aromatic_atom aromatic_os_num
aromatic_ring_5 -> starting_aromatic_c_num aromatic_atom full_aromatic_segment aromatic_os_num
full_aromatic_segment -> aromatic_atom aromatic_atom
aromatic_os -> side_aliphatic_ring
side_aliphatic_ring -> 'c' num '(' cycle_bond ')'
full_aromatic_segment -> side_aliphatic_ring_segment
side_aliphatic_ring_segment -> 'c' num 'c' '(' cycle_bond ')'
side_aliphatic_ring_segment -> 'c' '(' cycle_bond ')' 'c' num
valence_4_num -> 'C' num
valence_4_num -> '[' 'C' '@' ']' num
valence_4_num -> '[' 'C' '@' '@' ']' num
valence_4_num -> '[' 'N' '+' ']' num
valence_3_num -> '[' 'C' '@' 'H' ']' num
valence_3_num -> '[' 'C' '@' '@' 'H' ']' num
valence_3_num -> 'N' num
valence_3_num -> '[' 'N' 'H' '+' ']' num
valence_3_num -> valence_4_num
valence_3_num -> valence_4_num '(' nonH_bond ')'
valence_2_num -> 'O' num
valence_2_num -> 'S' num
valence_2_num -> 'S' num '(' '=' 'O' ')' '(' '=' 'O' ')'
valence_2_num -> valence_3_num
valence_2_num -> valence_3_num '(' nonH_bond ')'
valence_2_num -> valence_4_num '(' double_bond ')'
num -> '1'
num1 -> '1'
num -> '2'
num1 -> '2'
num -> '3'
num1 -> '3'
num -> '4'
num1 -> '4'
num -> '5'
num1 -> '5'
num -> '6'
num1 -> '6'
num -> '7'
num1 -> '7'
num -> '8'
num1 -> '8'
num -> '9'
num1 -> '9'
num -> '%10'
num1 -> '%10'
num -> '%11'
num1 -> '%11'
...
num -> '%49'
num1 -> '%49'
\end{verbatim}
